buildscript {
    repositories {
        if (project.hasProperty('local')) {
            println("Deferring to local dependency resolution")
            mavenLocal()
        }
        maven { url property('nexusAggregateUrl') }
    }
    dependencies {
        classpath 'impact.ciutils:vault:2.0.1-SNAPSHOT'
        classpath 'de.undercouch:gradle-download-task:3.2.0'
        classpath "org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_2.11:1.0.1"
        classpath "com.palantir.gradle.docker:gradle-docker:0.25.0"
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "me.champeau.gradle.jmh" version "0.5.0"
    // Needed for dependencies with different binaries based on ${osdetector.classifier}
    id "com.google.osdetector" version "1.6.2"
}

ext {
    // Dataproc libs needed at compile that must match dataproc server
    bigtableVer = "1.12.0"
    gcsConnectorVer = "hadoop2-1.9.9"
    gcsStorageVer = "1.64.0"
    hadoopVer = "2.9.2"
    handlebarsVer = "4.2.0"
    hbaseVer = "2.1.4"
    scalaCompatVer = "_2.11"
    scalaVer = "2.11.12"
    sparkVer = "2.4.5"
    sparkJavaVer = "2.5"

    // Platform lib needs these
    hdrHistogramVer = "2.1.9"
    rollingHistogramVer = "2.0.4"
    typesafeConfigVer = "1.3.2"
    failsafeVer = "2.0.1"

    // Thirdparty libs
    antVer = "1.9.1"
    apacheCommonsPool2Ver = "2.7.0"
    avroVer = "1.8.2"
    bigQueryTransferVer = "0.120.0-beta"
    bigQueryVer = "1.87.0"
    commonsCodecVer = "1.11"
    commonsIoVer = "2.4"
    commonsLang3Ver = "3.5"
    commonsNetVer = "3.3"
    commonsPool2Ver = "2.7.0"
    commonsValidatorVer = "1.6"
    confluentVer = "6.0.0"
    dropwizardMetricsVer = "3.1.2"
    groovyVer = "2.4.4:indy"
    guavaVer = "20.0"
    hiveVer = "1.2.1"
    h2Ver = "1.4.199"
    impalaJdbcMajorVer = "41"
    impalaJdbcMinorVer = "2.6.15.1017"
    jacksonVer = "2.6.0"
    jedisVer = "3.1.0-m4"
    jmhVer = "1.17.4" // keep this on 1.17.4. We tried 1.21 which increased jmh benchmark from ~6min to ~1hr
    jodaVer = "2.9.9"
    kuduTestUtilsVer = "1.12.0"
    kuduTestBinaryVer = "${ext.kuduTestUtilsVer}:${osdetector.classifier}"
    mysqlConnectVer = "8.0.18"
    nettyVer = "4.1.22.Final"
    parquetAvroVer = "1.6.0"
    parquetToolsVer = "1.10.1"
    prometheusVer = "0.0.23"
    redisClientVer = "3.10"
    scalaNullSafeVer = "1.2.4"
    servletApi = "3.1.0"
    slf4jVer = "1.7.4"
    sparkRedisVer = "2.3.1"
}

rootProject.version = revision

configure(subprojects.findAll { it.name != 'python' }) {
    project.version = revision

    repositories {
        if (project.hasProperty('local')) {
            println("Deferring to local dependency resolution")
            mavenLocal()
        }
        maven { url property('nexusAggregateUrl') }
        flatDir { dirs "$rootDir/deps/libs" }
    }

    configurations {
        compileAndJar
        compile { extendsFrom compileAndJar }
        lib
    }

    dependencies {
        compile("org.scala-lang:scala-library:$scalaVer")
        testCompile("org.scalatest:scalatest$scalaCompatVer:3.0.4")
        testCompile("org.mockito:mockito-core:1.9.5")
        testCompile("org.junit.jupiter:junit-jupiter-api:5.7.1")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:5.7.1")
        testRuntime("org.junit.platform:junit-platform-engine:1.7.1")
        testRuntime("org.junit.platform:junit-platform-launcher:1.7.1")
        testRuntime("co.helmethair:scalatest-junit-runner:0.1.9")
    }
}
